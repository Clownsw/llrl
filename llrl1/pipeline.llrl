(import "std/ord-map" _)
(import "std/hash-set" _)
(import "std/io" [io/_ _])
(import "std/system" process/_)
(import "~/util" _)
(import "~/path" Path _.Path PackageName package-name/_)
(import "~/source-location" SourceLocationTable source-location-table/_)
(import "~/ast" [ast/_ _])
(import "~/report" _.Report report/_)
(import "~/code" [code/_ _] code-set/_)
(import "~/module" [module/_ _] module/_)
(import "~/emitter" _.Emitter backend/_ emitter/_)
(import "~/backend/llvm" _.LLVMBackend llvm-backend/_ with-llvm-backend)
(export Error _.Error error:_ Pipeline _.Pipeline pipeline/_)

(value-data Error
  (error:code (Array (: Path code/Error)))
  (error:circular-dependencies (Array Path))
  (error:module SourceLocationTable (OrdMap module/ModuleId module/Module) Path module/Error)
  (error:clang String))

(instance Display.Error (Display Error)
  (function (display! w a)
    (match a
      [(error:code (let code-errors))
        (for (: (let path) (let e)) code-errors
          (display-all! w path ": " e "\n")!)
        (ok unit)]
      [(error:circular-dependencies (let paths))
        (display! w "Circular dependencies detected:\n")!
        (for (let path) paths
          (display-all! w "- " path "\n")!)
        (display! w "Modules cannot import each other.\n")]
      [(error:module (let slt) (let module-set) (let path) (let error))
        (display-all! w (contextual: error (module/display-context module-set slt)) " (while building " path ")\n")]
      [(error:clang (let e))
        (display-all! w "Failed to compile with clang: " e "\n")])))

(derive (DebugDisplay) data Pipeline
  (pipeline:
    (code/Loader ast/Construct)
    (Vector Path)
    (Vector String)
    (Ref Bool)
    (Ref Bool)))

(getter pipeline:
  code-loader
  entry-code-paths
  clang-options
  optimize?
  verbose?)

(function (pipeline/new current-path) {(-> String Pipeline)}
  (let1 loader (code/loader/new)
    (code/loader/add-embedded-builtin! loader)
    (code/loader/add-embedded-std! loader)
    (code/loader/add-package-filepath! package-name/current current-path loader)
    (pipeline: loader vector/empty vector/empty (ref #f) (ref #f))))

(function (pipeline/register-package! name filepath p) {(-> PackageName String Pipeline Bool)}
  (code/loader/add-package-filepath! name filepath (code-loader p)))

(function (pipeline/add-entry-path! path p) {(-> Path Pipeline unit)}
  (vector/push! path (entry-code-paths p)))

(function (pipeline/set-optimize! enabled p) {(-> Bool Pipeline unit)}
  (set! (optimize? p) enabled))

(function (pipeline/set-verbose! enabled p) {(-> Bool Pipeline unit)}
  (set! (verbose? p) enabled))

(function (pipeline/add-clang-option! option p) {(-> String Pipeline unit)}
  (vector/push! option (clang-options p)))

(function (pipeline/run! output run-args p)
  {(-> String (Option (Array String)) Pipeline (Result (Option I32) Error))}
  (let* ([output (io/path "." output)]
         [slt (source-location-table/new)]
         [report (report/new)]
         [code-set (code-set/build! (entry-code-paths p) (code-loader p) slt report)]
         [code-errors (collect (code-set/errors code-set))]
         [_ (when (not (array/empty? code-errors)) (err (error:code code-errors))!)]
         [codes (result/map-err error:circular-dependencies (code-set/resolve-dependencies-order code-set))!])
    (with-llvm-backend ~(optimize? p) ~(verbose? p) (lambda (backend)
      (let ([emitter (emitter/new backend)]
            [entry-points (hash-set/collect (entry-code-paths p))])
        (match (module/build! (vector/buffer codes) entry-points emitter report)
          [(: (let module-set) none)
            (emitter/complete! report emitter)
            (result/map-err error:clang (llvm-backend/produce-executable! output (vector/buffer (clang-options p)) backend))!
            (backend/complete! report backend)
            (ok unit)]
          [(: (let module-set) (some (: (let path) (let error))))
            (err (error:module slt module-set path error))]))))!
    (ok
      (with1? (some (let run-args)) run-args
        (|> (process/execute! output run-args)
            (result/unwrap* [^1 (assert #f (string "Failed to execute process: " %1))])
            (option/unwrap* [^0 (assert #f "Failed to complete process")]))))))
