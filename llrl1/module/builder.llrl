(import "std/hash-set" _)
(import "std/hash-map" _)
(import "std/ord-map" _)
(import "~/util" _)
(import "~/path" Path _.Path path/_)
(import "~/sexp" S _.S)
(import "~/ast" _)
(import "~/report" Report report/_ phase:_)
(import "~/source" Source _.Source source/_ source-rep:_)
(import "~/module/error" _)
(import "~/module/meaning" imports/add!)
(import "~/module/unit" _)
(import "~/module/builder/external" External external/_)
(import "~/module/builder/importer" [importer/run! run!])
(import "~/module/builder/ast-builder" [ast-builder/run! run!])
(import "~/module/builder/resolver" [resolver/run! run!])
(import "~/module/builder/kind-inference" [kind-inference/run! run!])
(import "~/module/builder/type-inference" [type-inference/run! run!])
(import "~/module/builder/validator" [validator/run! run!])
(import "~/module/builder/exporter" [exporter/run! run!])
(export Backend Backend.unit backend/_ module/build!)

(class (Backend B)
  (function (backend/add-module! module entry-point? b) {(-> Module Bool B unit)})
  (function (backend/execute-macro! id s e) {(-> (NodeId Macro) (S Construct) B (Result (S Construct) String))}))

(instance Backend.unit (Backend unit)
  (function (backend/add-module! module entry-point? b))
  (function (backend/execute-macro! id s e) (err "Unsupported")))

; Build a set of modules from the sources. The sources must be sorted by dependency order.
(function (module/build! sources entry-points backend report)
  {(forall B)
   (-> (Array (Source Construct)) (HashSet Path) B Report (: (OrdMap ModuleId Module) (Option (: Path Error))))
   (where (Backend B))}
  (when (array/empty? sources) (return (: ord-map/empty none)))

  ; (module-id 0) must be builtin
  (assert-eq? (source/path (get sources[0])) path/builtin)
  ; Every other module must depend on builtin
  (assert (it/all? [^1 (ord-map/has? (string path/builtin) (source/dependencies %1))] (it/skip 1 sources)))
    ; Every implicit-std? enabled module must depend on std
  (assert (it/all? [^1 (or (not (source/implicit-std? %1))
                           (ord-map/has? (string path/std) (source/dependencies %1)))] sources))

  (let* ([modules ord-map/empty]
         [path-to-module hash-map/empty]
         [external (module-build-context: modules path-to-module backend)])
    (report/on report phase:build-module
      (for (: (let index) (let source)) (it/with-index sources)
        (let* ([path (source/path source)]
               [mid (module-id/from-index (conv index))]
               [module (module/new-uninitialized mid path)]
               [result (semantic-analysis! module source external report)])
          (report/merge! (module/report module) report)
          (ord-map/insert! mid module modules)
          (when-match1 (err (let e)) result (return (: modules (some (: path e)))))
          (hash-map/insert! path mid path-to-module)
          (backend/add-module! module (hash-set/has? path entry-points) backend))))
    (: modules none)))

(value-data (ModuleBuildContext B)
  (module-build-context: (OrdMap ModuleId Module) (HashMap Path ModuleId) B))

(instance External.ModuleBuildContext (forall B) (External (ModuleBuildContext B)) (where (Backend B))
  (function (external/module? mid e)
    (with1 (module-build-context: (let modules) _ _) e
      (ord-map/get? mid modules)))

  (function (external/module-by-path? path e)
    (with1 (module-build-context: (let modules) (let path-map) _) e
      (ord-map/get? (hash-map/get? path path-map)? modules)))

  (function (external/execute-macro! id s e)
    (with1 (module-build-context: _ _ (let backend)) e
      (backend/execute-macro! id s backend))))

(function (semantic-analysis! module source external report)
  {(forall E) (-> Module (Source Construct) E Report (Result unit Error)) (where (External E))}

  ; source/dependencies (Path) -> module/imports (ModuleId)
  (for (let path) (|> source source/dependencies ord-map/values)
    (match (external/module-by-path? path external)
      [(some (let module))
        (imports/add! (module/id module) (module/imports module))]
      [none
        (err (error:cannot-find-module path))!]))

  (match (source/rep source)
    [source-rep:empty]
    [(source-rep:code (let code))
      (report/on report phase:import (importer/run! module code source external))!
      (report/on report phase:build-ast (ast-builder/run! module code external))!
      (report/on report phase:resolve (resolver/run! module external))!
      (report/on report phase:kind-inference (kind-inference/run! module external))!
      (report/on report phase:type-inference (type-inference/run! module external))!
      (report/on report phase:validate (validator/run! module external))!
      (report/on report phase:export (exporter/run! module code))!
      ])
  (ok unit))
